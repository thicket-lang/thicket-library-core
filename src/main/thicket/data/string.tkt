/*
 * Thicket
 * https://github.com/d-plaindoux/thicket
 *
 * Copyright (c) 2015-2016 Didier Plaindoux
 * Licensed under the LGPL2 license.
 */
 
module Data.String

from System.Internal import internal

from Data.Boolean import bool,true,false
from Data.Native import native
from Data.Number import number
from Data.Character import char
from Data.Boolean import bool, true, false
from Data.Option import option,none,some
from Data.Comparable import comparable
from Data.Hashable import hashable
from Data.Matcher import matcher
from Data.Sequence import sequence, modifiable
from Data.List import emptyList, list
from Data.Array import emptyArray, array
from Lang.When import when

class string this:native {
    with comparable[string]
    with hashable[string]
    with matcher[string]
    with sequence[char]
    with modifiable[char,string]
        
    get : number -> option[char]
    set : number -> char -> string

    (+) : string -> string   
    (*) : number -> string    
    
    subStringAt : sequence[char] -> number -> bool
    
    toNumber : option[number] 
    toChars  : array[char]
    toList   : list[char]
} {
    def unbox = self

    def (==) n = internal "generic.==" self n true false
    def (<?) n = internal "generic.<" self n true false
    
    def hash = number $ internal "string.hash" self

    def get n = internal "string.getAt" self n (n -> some $ char n) none
    def set n v = string $ internal "string.setAt" self n v
    
    def (+) s = string $ internal "string.+" self s
    def (*) n = 1 to n fold "" (r _ -> self + r)
    
    def length = number $ internal "string.length" self    
                
    def subStringAt s offset =
        0 to (self.length--) fold 
            (some true)
            (r i -> 
                for _  <- r
                    c1 <- { self get i }
                    c2 <- { s get $ offset + i }
                if (c1 == c2)
                yield true) 
                orElse false
        
    def toNumber = internal "string.toNumber" self (n -> some $ number n) none
    def toChars = 
        let update = r i -> self get i fold r (r set i) in
            0 to (self.length --) fold (emptyArray self.length) update
    def toList = 
        let update = l i -> self get i fold l (l+:) in
            (self.length --) downto 0 fold emptyList update
}

/**
 * Adapters
 */

def adapter string2number : string -> option[number] = string::toNumber
def adapter string2array  : string -> array[char]    = string::toChars
def adapter string2list   : string -> list[char]     = string::toList
